import java.nio.file.Files
import java.nio.file.Paths

plugins {
  id 'com.adarshr.test-logger' version '1.7.0'
  id "com.github.johnrengelman.shadow" version "5.1.0"
  id "com.palantir.graal" version "0.6.0-5-g77e5e9f"
}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "com.palantir.graal"

group = 'jiracli'

sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
  mavenCentral()
  maven { url 'https://repo.spring.io/snapshot' }
  maven { url 'https://repo.spring.io/milestone' }
  maven { url 'https://maven.atlassian.com/content/repositories/atlassian-public/' }
  maven { url 'http://repo.spring.io/plugins-release/' }
}


dependencies {
  compileOnly group: 'com.oracle.substratevm', name: 'svm', version: '19.2.0.1'

  implementation 'info.picocli:picocli:4.0.4'
  annotationProcessor 'info.picocli:picocli-codegen:4.0.4'

  compileOnly 'org.projectlombok:lombok:1.18.10'
  testCompileOnly 'org.projectlombok:lombok:1.18.10'
  annotationProcessor 'org.projectlombok:lombok:1.18.10'
  testAnnotationProcessor 'org.projectlombok:lombok:1.18.10'

  //Commons
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
  implementation group: 'one.util', name: 'streamex', version: '0.6.8'
  implementation group: 'com.evanlennick', name: 'retry4j', version: '0.15.0'
  implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.9'

  //Jira-Cli specifics
  implementation group: 'com.mashape.unirest', name: 'unirest-java', version: '1.4.9'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.11.2'
  implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.2'

  implementation group: 'org.fusesource.jansi', name: 'jansi', version: '1.18'
  implementation 'de.vandermeer:asciitable:0.3.2'

  testImplementation group: 'junit', name: 'junit', version: '4.12'
  testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.13.2'
  testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'
  testImplementation group: 'com.github.javafaker', name: 'javafaker', version: '0.17.2'
  testImplementation group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.1'
  testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

testlogger {
  theme 'mocha'
  showSkipped true
}

test {
  ignoreFailures = true

  finalizedBy jacocoTestReport
}

compileJava {
  options.compilerArgs += ["-Aproject=${project.group}/${project.name}"]
}

jar {
  manifest {
    attributes(
        'Main-Class': 'jiracli.CheckSum'
    )
  }
}

shadowJar {
  archiveName = "jira-cli.jar"
}

//This is a hack. For some reason, -J-Djava.security.properties=java.security.overrides
//cannot see relative paths
nativeImage {
  doFirst {
    Files.copy(Paths.get("graal/java.security.overrides"),
        Paths.get("java.security.overrides"))
  }

  doLast {
    new File("java.security.overrides").delete()
  }
}

graal {
  mainClass 'jiracli.CheckSum'
  outputName "jira-cli-${System.getProperty("os.name").toLowerCase().replace(' ', '_') + '-' + System.getProperty("os.arch")}"
  option '--no-server'
  option '--enable-https'
  option '--enable-all-security-services'
  option '--no-fallback'
  option '--allow-incomplete-classpath'
  option '--report-unsupported-elements-at-runtime'
  option '-DremoveUnusedAutoconfig=true'
  option "-J-Djava.security.properties=java.security.overrides"
  option '-H:EnableURLProtocols=https'
  option '-H:+ReportExceptionStackTraces'
  option "-H:ReflectionConfigurationFiles=${buildDir}/classes/java/main/META-INF/native-image/picocli-generated/jiracli/jira-cli/reflect-config.json,graal/extra-reflect.json"
  option '--initialize-at-run-time=org.apache.http.Header,org.apache.http.params.HttpParams,org.apache.http.StatusLine,org.apache.http.ProtocolVerision,org.apache.http.client.methods.Closeable,org.apache.http.client.methods.CloseableHttpResponse,org.apache.http.ProtocolVersion,org.apache.http.HttpEntity,io.atlassian.fugue.Suppliers'
  option '-J-Djava.security.properties=java.security.overrides'
}

